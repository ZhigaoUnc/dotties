import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Io
import "Singletons"

Item {
    id: panel
    signal closeRequested()
    signal pickSaveDir()

    readonly property color glassBg: Theme.panelBg
    readonly property color glassBorder: Theme.panelBorder
    readonly property color vermilion: Theme.vermilion
    readonly property color idle: Theme.idle

    readonly property int arrow: 7
    implicitWidth: card.implicitWidth
    implicitHeight: card.implicitHeight + arrow

    transformOrigin: Item.Bottom
    opacity: visible ? 1 : 0
    scale: visible ? 1 : 0.96
    Behavior on opacity { NumberAnimation { duration: 140; easing.type: Easing.OutCubic } }
    Behavior on scale { NumberAnimation { duration: 140; easing.type: Easing.OutCubic } }

    component Section: ColumnLayout {
        Layout.fillWidth: true
        spacing: 6
    }

    component Label: Text {
        color: panel.idle
        font.family: Theme.sansFamily
        font.pixelSize: 13
    }

    component GroupLabel: Text {
        Layout.fillWidth: true
        Layout.bottomMargin: 2
        color: Theme.dimIcon
        font.family: Theme.monoFamily
        font.pixelSize: 10
        font.bold: true
        font.letterSpacing: 1.4
        font.capitalization: Font.AllUppercase
    }

    component Value: Text {
        color: panel.vermilion
        font.family: Theme.monoFamily
        font.pixelSize: 13
        font.bold: true
    }

    component Divider: Rectangle {
        Layout.fillWidth: true
        Layout.preferredHeight: 1
        Layout.topMargin: 4
        Layout.bottomMargin: 4
        color: Theme.sep
    }

    component Toggle: Item {
        id: tg
        property bool checked: false
        signal toggled(bool v)

        implicitWidth: 38
        implicitHeight: 22

        Rectangle {
            anchors.fill: parent
            radius: height / 2
            color: tg.checked ? panel.vermilion : Qt.rgba(1, 1, 1, 0.10)
            border.color: tg.checked ? panel.vermilion : panel.glassBorder
            border.width: 1
            Behavior on color { ColorAnimation { duration: 120 } }

            Rectangle {
                width: 16
                height: 16
                radius: 8
                anchors.verticalCenter: parent.verticalCenter
                x: tg.checked ? parent.width - width - 3 : 3
                color: Theme.white
                Behavior on x { NumberAnimation { duration: 140; easing.type: Easing.OutCubic } }
            }
        }

        TapHandler { onTapped: tg.toggled(!tg.checked) }
    }

    component Slider: Item {
        id: slider
        property int from: 0
        property int to: 100
        property int value: 0
        signal moved(int v)
        signal committed(int v)

        Layout.fillWidth: true
        implicitHeight: 22

        readonly property real frac: to > from ? (value - from) / (to - from) : 0
        readonly property real travel: Math.max(0, width - knob.width)

        function valueAtX(px) {
            var f = travel > 0 ? Math.max(0, Math.min(1, (px - knob.width / 2) / travel)) : 0;
            return Math.round(from + f * (to - from));
        }

        function setFromX(px) {
            var v = slider.valueAtX(px);
            if (v !== value) slider.moved(v);
        }

        Rectangle {
            id: track
            anchors.verticalCenter: parent.verticalCenter
            width: parent.width
            height: 4
            radius: 2
            color: Qt.rgba(1, 1, 1, 0.10)

            Rectangle {
                width: parent.width * slider.frac
                height: parent.height
                radius: 2
                color: panel.vermilion
            }
        }

        Rectangle {
            id: knob
            width: 14
            height: 14
            radius: 7
            anchors.verticalCenter: parent.verticalCenter
            x: slider.frac * slider.travel
            color: drag.active ? panel.vermilion : Theme.white
            border.color: panel.vermilion
            border.width: 2
        }

        TapHandler {
            onTapped: (p) => {
                slider.setFromX(p.position.x);
                slider.committed(slider.valueAtX(p.position.x));
            }
        }
        DragHandler {
            id: drag
            target: null
            onCentroidChanged: if (active) slider.setFromX(centroid.position.x)
            onActiveChanged: if (!active) slider.committed(slider.value)
        }
    }

    Rectangle {
        id: card
        width: parent.width
        height: parent.height - panel.arrow
        radius: 10
        color: panel.glassBg
        border.color: panel.glassBorder
        border.width: 1
        implicitWidth: 280
        implicitHeight: content.implicitHeight + 32

        ColumnLayout {
            id: content
            anchors.left: parent.left
            anchors.right: parent.right
            anchors.verticalCenter: parent.verticalCenter
            anchors.leftMargin: 18
            anchors.rightMargin: 18
            spacing: 14

            Text {
                text: "settings"
                color: Theme.white
                font.family: Theme.sansFamily
                font.pixelSize: 14
                font.weight: Font.Medium
                Layout.bottomMargin: 2
            }

            GroupLabel { text: "Save" }

            RowLayout {
                Layout.fillWidth: true
                Label { text: "Copy to clipboard" }
                Item { Layout.fillWidth: true }
                Toggle {
                    checked: Config.copyOnSave
                    onToggled: (v) => { Config.copyOnSave = v; Config.save(); }
                }
            }

            RowLayout {
                Layout.fillWidth: true
                Label { text: "Save to disk" }
                Item { Layout.fillWidth: true }
                Toggle {
                    checked: Config.copyToDisk
                    onToggled: (v) => { Config.copyToDisk = v; Config.save(); }
                }
            }

            RowLayout {
                Layout.fillWidth: true
                spacing: 10

                Text {
                    Layout.fillWidth: true
                    text: Config.saveDir !== "" ? Config.saveDir : "~/Pictures/Screenshots"
                    color: Theme.dimIcon
                    font.family: Theme.monoFamily
                    font.pixelSize: 11
                    elide: Text.ElideMiddle
                }

                Rectangle {
                    id: pickBtn
                    Layout.preferredHeight: 28
                    Layout.preferredWidth: pickLabel.implicitWidth + 22
                    radius: 6
                    color: pickHover.hovered ? Qt.rgba(1, 1, 1, 0.10) : Qt.rgba(1, 1, 1, 0.06)
                    border.color: panel.glassBorder
                    border.width: 1

                    Text {
                        id: pickLabel
                        anchors.centerIn: parent
                        text: "Choose…"
                        color: panel.idle
                        font.family: Theme.monoFamily
                        font.pixelSize: 12
                    }

                    HoverHandler { id: pickHover }
                    TapHandler { onTapped: panel.pickSaveDir() }
                }
            }

            Divider {}

            GroupLabel { text: "Effects" }

            Section {
                RowLayout {
                    Layout.fillWidth: true
                    Label { text: "Pixelate" }
                    Item { Layout.fillWidth: true }
                    Value { text: Config.mosaicFactor }
                }
                Slider {
                    from: 4
                    to: 40
                    value: Config.mosaicFactor
                    onMoved: (v) => Config.mosaicFactor = v
                    onCommitted: Config.save()
                }
            }

            Section {
                RowLayout {
                    Layout.fillWidth: true
                    Label { text: "Blur" }
                    Item { Layout.fillWidth: true }
                    Value { text: Config.blurRadius }
                }
                Slider {
                    from: 8
                    to: 128
                    value: Config.blurRadius
                    onMoved: (v) => Config.blurRadius = v
                    onCommitted: Config.save()
                }
            }
        }
    }

    Canvas {
        width: panel.arrow * 2
        height: panel.arrow
        anchors.top: card.bottom
        anchors.horizontalCenter: card.horizontalCenter
        onPaint: {
            var ctx = getContext("2d");
            ctx.reset();
            ctx.beginPath();
            ctx.moveTo(0, 0);
            ctx.lineTo(width, 0);
            ctx.lineTo(width / 2, height);
            ctx.closePath();
            ctx.fillStyle = Theme.panelBg;
            ctx.fill();
        }
    }
}
